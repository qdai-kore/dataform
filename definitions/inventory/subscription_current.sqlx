config {
    type: "incremental",
    uniqueKey: ["subscription_urn"],
    schema: 'inventory',
    bigquery:{
      clusterBy: ["org_urn","service_type_id","state","subscription_urn"]
    }
}

js {
  const last_n_days = dataform.projectConfig.vars.incremental_days + 1;
  // Get start of previous day
  const prevDay = constants.getStartOfPreviousDay(last_n_days);

}

-- after run, delete any sims are removed
post_operations {
  	delete from ${self()} where subscription_id in (
	    select subscription_id  
      from etl_cpro.etl_inv_subscription 
      where  etl_update >'${prevDay}' and etl_action ='Delete'
	)
}

select array_agg(t order by etl_update desc limit 1)[offset(0)].*
from etl_cpro.etl_inv_subscription t
${ when(incremental(), `WHERE etl_update >= '${prevDay}'`, `WHERE etl_update>='2023-06-01'`) }
group by t.subscription_urn