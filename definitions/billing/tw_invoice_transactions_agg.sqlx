config { 
    type: 'incremental',
    schema: 'billing',
    tags: ['twilio','invoice'],
    uniqueKey: ["source_platform_code","invoice_billing_period","invoice_account_primary_id","sku_primary_id","owner_account_primary_id","transaction_direction","sku_key","sku_value","event_time","invoice_stream_type"],
    bigquery:{
      partitionBy:"DATE_TRUNC(event_time, MONTH)",
      clusterBy:["source_platform_code","invoice_billing_period","invoice_account_primary_id","sku_primary_id"]
    }
}

js {
  const last_n_min = dataform.projectConfig.vars.incremental_min;
  // schedule every 30 min to get last 45 min of data, giving 15 min for edge data
  const prevRunTime = constants.getPreviousMin(last_n_min);

}


with grouped_eom as 
(
  select 
    t.source_platform_code, 
    t.invoice_billing_period,
    DATE_TRUNC(t.event_time,HOUR) as event_time,
    t.invoice_account_primary_id,
    t.sku_primary_id,
    t.owner_account_primary_id,
    t.transaction_direction,
    sku_key_values.key as sku_key,
    sku_key_values.value as sku_value,
    sum(total) as total_revenue,
    sum(data_quantity) as total_quantity
  from ${ref("billing","tw_invoice_transactions_eom")} t
    LEFT JOIN UNNEST(t.extended_sku_grouping) sku_key_values
    ${ when(incremental(), `WHERE etl_update >= '${prevRunTime}'`, `WHERE etl_update>='2023-10-01'`) }
  group by 
    t.source_platform_code, 
    t.invoice_billing_period,
    DATE_TRUNC(t.event_time,HOUR),
    t.invoice_account_primary_id,
    t.sku_primary_id,
    t.owner_account_primary_id,
    t.transaction_direction,
    sku_key_values.key,
    sku_key_values.value
),
grouped_jit as 
(
  select 
    t.source_platform_code, 
    t.invoice_billing_period,
    DATE_TRUNC(t.event_time,HOUR) as event_time,
    t.invoice_account_primary_id,
    t.sku_primary_id,
    t.owner_account_primary_id,
    t.transaction_direction,
    sku_key_values.key as sku_key,
    sku_key_values.value as sku_value,
    sum(total) as total_revenue,
    sum(data_quantity) as total_quantity
  from ${ref("billing","tw_invoice_transactions_jit")} t
    LEFT JOIN UNNEST(t.extended_sku_grouping) sku_key_values
    ${ when(incremental(), `WHERE etl_update >= '${prevRunTime}'`, `WHERE etl_update>='2023-10-01'`) }
  group by 
    t.source_platform_code, 
    t.invoice_billing_period,
    DATE_TRUNC(t.event_time,HOUR),
    t.invoice_account_primary_id,
    t.sku_primary_id,
    t.owner_account_primary_id,
    t.transaction_direction,
    sku_key_values.key,
    sku_key_values.value
)


select *, 'EOM' as invoice_stream_type from grouped_eom
union all
select *, 'JIT' as invoice_stream_type from grouped_jit


