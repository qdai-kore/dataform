config {
    type: "incremental",
    uniqueKey: ["org_urn","subscription_urn","usage_type_id","period_id","usage_date"],
    schema: 'usage'
}

js {
  const last_n_days = dataform.projectConfig.vars.incremental_days;
  // Get start of previous day
  const prevDay = constants.getStartOfPreviousDay(last_n_days);

}

select array_agg(t order by etl_update desc limit 1)[offset(0)].*
from ${ref('stg_subscription_usage')} t
${ when(incremental(), `WHERE etl_update >= '${prevDay}'`, `WHERE etl_update>='2023-06-01'`) }
group by t.org_urn, t.subscription_urn, t.usage_type_id, t.period_id, t.usage_date