config {
    type: "incremental",
	tags: ["radius_usage"],
    uniqueKey: ["calling_station_id"],
    schema: 'radius',
    bigquery: {
      clusterBy: ["calling_station_id"]
    }
}

js {
  const incremental_hour = dataform.projectConfig.vars.incremental_hour;
}

with group_msisdn as 
(
	select array_agg(t order by PARSE_TIMESTAMP("%b %e %Y %k:%M:%S UTC",JSON_EXTRACT_SCALAR(radius_raw,'$.EVENT-TIMESTAMP')) desc limit 1)[offset(0)].*
	from etl_cpro.etl_radius_acct_rich t 
	${ when(incremental(), `WHERE etl_update >= TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL ${incremental_hour} HOUR)`, `WHERE etl_update>='2023-10-01'`) }
	and JSON_EXTRACT_SCALAR(radius_raw,'$.CALLING-STATION-ID') is not null
	group by JSON_EXTRACT_SCALAR(radius_raw,'$.CALLING-STATION-ID')
)
, extracted as
(
	select
	JSON_EXTRACT_SCALAR(radius_raw,'$.CALLING-STATION-ID') calling_station_id,
	JSON_EXTRACT_SCALAR(radius_raw,'$.CALLED-STATION-ID') called_station_id,
	JSON_EXTRACT_SCALAR(radius_raw,'$.ACCT-STATUS-TYPE') acct_status_type,
	JSON_EXTRACT_SCALAR(des_sim_info,'$.PlatformID') platform_id,
	JSON_EXTRACT_SCALAR(des_sim_info,'$.Subscription.SubscriptionURN') subscription_urn,
	JSON_EXTRACT_SCALAR(des_sim_info,'$.ICCID') iccid,
	PARSE_TIMESTAMP("%b %e %Y %k:%M:%S UTC",JSON_EXTRACT_SCALAR(radius_raw,'$.EVENT-TIMESTAMP')) event_timestamp
	from group_msisdn
)
select *,  
case when acct_status_type = 'Stop' then 'NotConnected' else 'Connected' end connection
from extracted
where calling_station_id is not null